// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Memory.hdl
/** 
 * The Memory chip implements the complete address space of the Hack data memory,
 * including RAM, memory mapped screen, and memory mapped keyboard.
 * Outputs the value of the memory location specified by the address input.
 * If (load == 1), sets the memory location specified by the address input
 * to the value of the in input.
 * Address space rules:
 * Only the upper 16K + 8K + 1 words of the memory are used.
 * Access to address 0 to 16383 (0x0000 to 0x3FFF) results in accessing the RAM;
 * Access to address 16384 to 24575 (0x4000 to 0x5FFF) results in accessing the Screen memory map;
 * Access to address 24576 (0x6000) results in accessing the Keyboard memory map.
 */ 
CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    // 16383 = 011111111111111
    // 16384 = 100000000000000

    // 24575 = 101111111111111
    // 24576 = 110000000000000

    PARTS:
    //// Replace this comment with your code.
    // kbsel = address[14] ^ address[13]
    And(a=address[14],b=address[13],out=kbsel);
    And(a=kbsel,b=load,out=loadkb);

    // screensel = address[14] and not address[13]
    Not(in=address[13],out=nr13);
    And(a=address[14],b=nr13,out=screensel);
    And(a=screensel,b=load,out=loadscreen);

    // ramsel = (not address[14] and not address[13]) or (not address[14] and address[13]) = not address[14]
    Not(in=address[14],out=ramsel);
    And(a=ramsel,b=load,out=loadram);
    
    // load data into different parts of RAM depending on selectors
    RAM16K(in=in,load=loadram,address=address[0..13],out=ramout);
    Screen(in=in,load=loadscreen,address=address[0..12],out=screenout);
    Keyboard(out=kbout);

    // pick between screenout and ramout using ramsel
    Mux16(a=screenout,b=ramout,sel=ramsel,out=ramorscreenout);
    // pick between ram or screen or kbout using kbsel
    Mux16(a=ramorscreenout,b=kbout,sel=kbsel,out=out);
}
